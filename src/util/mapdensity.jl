"""
    MapTargetDensity

Wrapper for target density functions used in transport map optimization.
Stores the log-density function and its gradient, with support for automatic
differentiation backends via DifferentiationInterface.jl.

# Fields
- `logdensity<:Function`: Function computing log-density `log π(x)`
- `ad_backend<:Union{Nothing,ADTypes.AbstractADType}`: AD backend or `nothing` for analytical
- `grad_logdensity<:Function`: Function computing gradient `∇ log π(x)`
- `prepared_gradient`: Optional prepared gradient for performance (can be `nothing`)
- `isvectorized::Bool`: Whether the log-density (and gradient, if analytical) accepts matrix input
- `threaded::Bool`: Whether to use multithreading for gradient evaluations on matrices (default: `true`)

# Constructors
- `MapTargetDensity(logdensity, grad_logdensity; isvectorized=false, threaded=true)`: Provide both log-density and analytical gradient.
- `MapTargetDensity(logdensity, backend::ADTypes.AbstractADType, d::Int; isvectorized=false, threaded=true)`: Use AD backend with prepared gradient.
- `MapTargetDensity(logdensity, backend::ADTypes.AbstractADType; isvectorized=false, threaded=true)`: Use AD backend without preparation.
- `MapTargetDensity(logdensity; isvectorized=false, threaded=true)`: Use ForwardDiff.

# Examples
```julia
# Use ForwardDiff (default, not prepared)
density = MapTargetDensity(logπ)

# Use ForwardDiff with prepared gradient (faster for repeated evaluations)
density = MapTargetDensity(logπ, AutoForwardDiff(), d)

# Use Mooncake with preparation
density = MapTargetDensity(logπ, AutoMooncake(), d)

# Use FiniteDiff
density = MapTargetDensity(logπ, AutoFiniteDiff(), d)

# Use analytical gradient
density = MapTargetDensity(logπ, grad_logπ)

# Use vectorized log-density (accepts matrix input where rows are samples)
density = MapTargetDensity(logπ_vectorized; isvectorized = true)
```
"""
struct MapTargetDensity{F <: Function, B <: Union{Nothing, ADTypes.AbstractADType}, G <: Function, P <: Union{Nothing, DifferentiationInterface.GradientPrep}} <: AbstractMapDensity
    logdensity::F
    ad_backend::B
    grad_logdensity::G
    prepared_gradient::P
    isvectorized::Bool
    threaded::Bool

    # Analytical gradient
    function MapTargetDensity(logdensity::F, grad_logdensity::G; isvectorized::Bool = false, threaded::Bool = true) where {F <: Function, G <: Function}
        return new{F, Nothing, G, Nothing}(logdensity, nothing, grad_logdensity, nothing, isvectorized, threaded)
    end

    # AD backend with prepared gradient
    function MapTargetDensity(logdensity::F, backend::B, d::Int; isvectorized::Bool = false, threaded::Bool = true) where {F <: Function, B <: ADTypes.AbstractADType}
        # Prepare gradient once for this input size
        prep = DifferentiationInterface.prepare_gradient(logdensity, backend, zeros(d))

        grad_logdensity = function (x)
            return DifferentiationInterface.gradient(logdensity, prep, backend, x)
        end

        if isa(backend, AutoMooncake)
            threaded = false
        end

        return new{F, B, typeof(grad_logdensity), typeof(prep)}(logdensity, backend, grad_logdensity, prep, isvectorized, threaded)
    end

    # AD backend without preparation
    function MapTargetDensity(logdensity::F, backend::B; isvectorized::Bool = false, threaded::Bool = true) where {F <: Function, B <: ADTypes.AbstractADType}
        grad_logdensity = function (x)
            return DifferentiationInterface.gradient(logdensity, backend, x)
        end

        if isa(backend, AutoMooncake)
            threaded = false
        end

        return new{F, B, typeof(grad_logdensity), Nothing}(logdensity, backend, grad_logdensity, nothing, isvectorized, threaded)
    end

    # Default: ForwardDiff without preparation
    function MapTargetDensity(logdensity::F; isvectorized::Bool = false, threaded::Bool = true) where {F <: Function}
        backend = AutoForwardDiff()
        grad_logdensity = function (x)
            return DifferentiationInterface.gradient(logdensity, backend, x)
        end
        return new{F, typeof(backend), typeof(grad_logdensity), Nothing}(logdensity, backend, grad_logdensity, nothing, isvectorized, threaded)
    end
end

"""
    MapReferenceDensity

Wrapper for reference density (typically standard Gaussian) used in transport maps.
The reference density defines the space from which samples are drawn and mapped
to the target distribution.

# Fields
- `logdensity<:Function`: Function computing log-density `log ρ(z)`
- `grad_logdensity<:Function`: Function computing gradient `∇ log ρ(z)` via `gradlogpdf` when available, otherwise via ForwardDiff
- `densitytype::Distributions.UnivariateDistribution`: Univariate density type (e.g., `Normal()`)

# Constructors
- `MapReferenceDensity()`: Use standard normal as reference density.
- `MapReferenceDensity(densitytype)`: Specify univariate distribution; uses analytical `gradlogpdf` when available, otherwise falls back to ForwardDiff.
"""
struct MapReferenceDensity{F <: Function, G <: Function} <: AbstractMapDensity
    logdensity::F
    grad_logdensity::G
    densitytype::Distributions.UnivariateDistribution

    function MapReferenceDensity(
            densitytype::Distributions.UnivariateDistribution = Normal()
        )
        density = x -> sum(logpdf.(Ref(densitytype), x))

        # Use gradlogpdf if available, otherwise fall back to ForwardDiff
        grad_density = if hasmethod(gradlogpdf, Tuple{typeof(densitytype), Float64})
            x -> gradlogpdf.(Ref(densitytype), x)
        else
            backend = AutoForwardDiff()
            x -> DifferentiationInterface.gradient(density, backend, x)
        end

        return new{typeof(density), typeof(grad_density)}(
            density, grad_density, densitytype
        )
    end
end

"""
    logpdf(density::AbstractMapDensity, x)

Evaluate the log-density at point(s) `x`.

# Arguments
- `density::AbstractMapDensity`: Target or reference density
- `x`: Point (vector) or multiple points (matrix, rows are samples) at which to evaluate

# Returns
- Scalar log-density value for vector input, or vector of log-densities for matrix input
"""
logpdf(density::AbstractMapDensity, x::Vector{<:Real}) = density.logdensity(x)

logpdf(density::AbstractMapDensity, x::Real) = logpdf(density, [x])

function logpdf(density::MapTargetDensity, X::Matrix{<:Real})

    if density.isvectorized
        return density.logdensity(X)

    else
        n = size(X, 1)
        logdensities = zeros(Float64, n)

        Threads.@threads for i in 1:n
            logdensities[i] = density.logdensity(view(X, i, :))
        end
        return logdensities
    end
end

function logpdf(density::MapReferenceDensity, X::Matrix{<:Real})
    n = size(X, 1)
    logdensities = zeros(Float64, n)

    Threads.@threads for i in 1:n
        logdensities[i] = density.logdensity(view(X, i, :))
    end
    return logdensities
end

"""
    grad_logpdf(density::AbstractMapDensity, x)

Evaluate the gradient of log-density at point(s) `x`.

# Arguments
- `density::AbstractMapDensity`: Target or reference density
- `x`: Point (vector) or multiple points (matrix, rows are samples) at which to evaluate

# Returns
- Gradient vector for vector input, or matrix of gradients (one per row) for matrix input
"""
grad_logpdf(density::AbstractMapDensity, x::Vector{<:Real}) = density.grad_logdensity(x)

function grad_logpdf(density::MapTargetDensity, X::Matrix{<:Real})

    if density isa MapTargetDensity && density.isvectorized && isnothing(density.ad_backend)
        return density.grad_logdensity(X)
    else
        n, d = size(X)
        log_gradients = zeros(Float64, n, d)

        if density.threaded
            Threads.@threads for i in 1:n
                log_gradients[i, :] = density.grad_logdensity(X[i, :])
            end
        else
            for i in 1:n
                log_gradients[i, :] = density.grad_logdensity(X[i, :])
            end
        end

        return log_gradients
    end
end

function grad_logpdf(density::MapReferenceDensity, X::Matrix{<:Real})
    n, d = size(X)
    log_gradients = zeros(Float64, n, d)

    Threads.@threads for i in 1:n
        log_gradients[i, :] = density.grad_logdensity(X[i, :])
    end

    return log_gradients
end

"""
    pdf(density::AbstractMapDensity, x)

Evaluate the probability density at point(s) `x`.

# Arguments
- `density::AbstractMapDensity`: Target or reference density
- `x`: Point (vector) or multiple points (matrix, rows are samples) at which to evaluate

# Returns
- Scalar density value for vector input, or vector of densities for matrix input

# Note
This computes `exp(logpdf(density, x))`. For numerical stability, prefer using `logpdf` when possible.
"""
pdf(density::AbstractMapDensity, x::Vector{<:Real}) = exp(logpdf(density, x))

pdf(density::AbstractMapDensity, x::Real) = pdf(density, [x])

pdf(density::AbstractMapDensity, X::Matrix{<:Real}) = exp.(logpdf(density, X))

function Base.show(io::IO, target::MapTargetDensity)
    backend_str = target.ad_backend === nothing ? "analytical" : string(target.ad_backend)
    return print(io, "MapTargetDensity(backend=$(backend_str))")
end

function Base.show(io::IO, ref::MapReferenceDensity)
    return print(io, "MapReferenceDensity(density=$(ref.densitytype))")
end
